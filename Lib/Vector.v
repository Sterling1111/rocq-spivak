From Lib Require Import Imports Limit Notations Reals_util Functions.
Import LimitNotations FunctionNotations.

Local Notation length := List.length.

Record vector (A : Type) (n : nat) := mk_vector {
  vlist : list A;
  vlist_length : length vlist = n
}.

Arguments mk_vector {A n} vlist vlist_length.
Arguments vlist {A n} _.

(** Structural operations on nonempty vectors need no default element. *)
Definition vector_cons {A n} (x : A) (v : vector A n) : vector A (S n) :=
  mk_vector (x :: vlist v) (f_equal S (vlist_length A n v)).

Definition vector_head {A n} (v : vector A (S n)) : A.
Proof.
  destruct v as [[|x xs] Hlength].
  - discriminate Hlength.
  - exact x.
Defined.

Definition vector_tail {A n} (v : vector A (S n)) : vector A n.
Proof.
  refine (mk_vector (List.tl (vlist v)) _).
  destruct v as [[|x xs] Hlength]; simpl in *; lia.
Defined.

Lemma vector_head_nth {A n} (v : vector A (S n)) (d : A) :
  vector_head v = List.nth 0 (vlist v) d.
Proof. destruct v as [[|x xs] Hlength]; [discriminate Hlength | reflexivity]. Qed.

Lemma vector_tail_nth {A n} (v : vector A (S n)) i (d : A) :
  List.nth i (vlist (vector_tail v)) d = List.nth (S i) (vlist v) d.
Proof. destruct v as [[|x xs] Hlength]; [discriminate Hlength | reflexivity]. Qed.

Lemma vector_eq {A : Type} {n : nat} (v1 v2 : vector A n) :
  vlist v1 = vlist v2 -> v1 = v2.
Proof.
  destruct v1 as [l1 H1], v2 as [l2 H2]. simpl. intros H. subst.
  f_equal. apply proof_irrelevance.
Qed.

Definition vector_map {A B : Type} {n : nat} (f : A -> B) (v : vector A n) : vector B n.
Proof.
  destruct v as [l Hl]. exists (map f l).
  rewrite length_map, Hl. reflexivity.
Defined.

Definition vector_map2 {A B C : Type} {n : nat} 
  (op : A -> B -> C) (v1 : vector A n) (v2 : vector B n) : vector C n.
Proof.
  destruct v1 as [l1 H1], v2 as [l2 H2].
  exists (map (fun p => op (fst p) (snd p)) (combine l1 l2)).
  rewrite length_map, length_combine, H1, H2.
  rewrite Nat.min_id. reflexivity.
Defined.

Definition vector_fold {A B : Type} {n : nat} (f : A -> B -> B) (b : B) (v : vector A n) : B :=
  fold_right f b (vlist v).

Class Zero (A : Type) := zero : A.
Class One  (A : Type) := one  : A.
Class Add  (A : Type) := add  : A -> A -> A.
Class Mul  (A : Type) := mul  : A -> A -> A.
Class Scale (S A : Type) := scale : S -> A -> A.
Class Le (A : Type) := le_op : A -> A -> Prop.
Class Lt (A : Type) := lt_op : A -> A -> Prop.
Class Ge (A : Type) := ge_op : A -> A -> Prop.
Class Gt (A : Type) := gt_op : A -> A -> Prop.

Instance Zero_R : Zero R := 0.
Instance One_R  : One R  := 1.
Instance Add_R  : Add R  := Rplus.
Instance Mul_R  : Mul R  := Rmult.
Instance Scale_R : Scale R R := Rmult.
Instance Le_R : Le R := Rle.
Instance Lt_R : Lt R := Rlt.
Instance Ge_R : Ge R := Rge.
Instance Gt_R : Gt R := Rgt.

Instance Zero_nat : Zero nat := 0%nat.
Instance One_nat  : One nat  := 1%nat.
Instance Add_nat  : Add nat  := Nat.add.
Instance Mul_nat  : Mul nat  := Nat.mul.
Instance Scale_nat : Scale nat nat := Nat.mul.
Instance Le_nat : Le nat := Peano.le.
Instance Lt_nat : Lt nat := Peano.lt.
Instance Ge_nat : Ge nat := Peano.ge.
Instance Gt_nat : Gt nat := Peano.gt.

Instance Zero_Vector {A} {n} `{Zero A} : Zero (vector A n) :=
  mk_vector (repeat zero n) (repeat_length zero n).

Instance Add_Vector {A} {n} `{Add A} : Add (vector A n) :=
  vector_map2 add.

Instance Mul_Vector {A} {n} `{Mul A} : Mul (vector A n) :=
  vector_map2 mul.

Instance Scale_Vector {S A} {n} `{Scale S A} : Scale S (vector A n) :=
  fun s v => vector_map (scale s) v.

Definition vector_dot {A} {n} `{Add A} `{Mul A} `{Zero A} (v1 v2 : vector A n) : A :=
  vector_fold add zero (vector_map2 mul v1 v2).

Definition vector_norm {n} (v : vector R n) : R :=
  sqrt (vector_dot v v).

Instance Le_Vector {A} {n} `{Le A} : Le (vector A n) :=
  fun v1 v2 => Forall2 le_op (vlist v1) (vlist v2).

Instance Lt_Vector {A} {n} `{Lt A} : Lt (vector A n) :=
  fun v1 v2 => Forall2 lt_op (vlist v1) (vlist v2).

Instance Ge_Vector {A} {n} `{Ge A} : Ge (vector A n) :=
  fun v1 v2 => Forall2 ge_op (vlist v1) (vlist v2).

Instance Gt_Vector {A} {n} `{Gt A} : Gt (vector A n) :=
  fun v1 v2 => Forall2 gt_op (vlist v1) (vlist v2).

Module VectorNotations.
  Declare Scope V_Scope.
  Delimit Scope V_Scope with V.

  Notation "⟨ ⟩" := (mk_vector nil eq_refl) : V_Scope.
  (* Keep dimensions independent of entries: length-of-list types can cause
     unification to assign overloaded 0/1 entries while matching row lengths. *)
  Notation "⟨ x ⟩" := (vector_cons x (mk_vector nil eq_refl)) : V_Scope.
  Notation "⟨ x , .. , y ⟩" :=
    (vector_cons x .. (vector_cons y (mk_vector nil eq_refl)) ..) : V_Scope.

  Notation "v1 + v2" := (add v1 v2) (at level 50, left associativity) : V_Scope.
  Notation "v1 ⊙ v2" := (mul v1 v2) (at level 40, left associativity) : V_Scope.
  Notation "v1 · v2" := (vector_dot v1 v2) (at level 40, left associativity) : V_Scope.
  Notation "r * v"   := (scale r v) (at level 40, left associativity) : V_Scope.
  Notation "∥ v ∥"   := (vector_norm v) (at level 40) : V_Scope.

  Notation "v1 <= v2" := (le_op v1 v2) (at level 70, no associativity) : V_Scope.
  Notation "v1 < v2" := (lt_op v1 v2) (at level 70, no associativity) : V_Scope.
  Notation "v1 >= v2" := (ge_op v1 v2) (at level 70, no associativity) : V_Scope.
  Notation "v1 > v2" := (gt_op v1 v2) (at level 70, no associativity) : V_Scope.
  
  Notation "'0'" := zero : V_Scope.
  Notation "'1'" := one : V_Scope.

End VectorNotations.

Import VectorNotations.

Ltac auto_op := 
  try unfold zero in *; try unfold one in *; 
  try unfold Zero_R in *; try unfold One_R in *; 
  try unfold Zero_nat in *; try unfold One_nat in *; 
  try unfold Zero_Vector in *;
  try unfold le_op, lt_op, ge_op, gt_op, Le_R, Lt_R, Ge_R, Gt_R, 
       Le_nat, Lt_nat, Ge_nat, Gt_nat, Le_Vector, Lt_Vector, Ge_Vector, Gt_Vector in *;
  try lra; try nra; try lia; try reflexivity.

(** Concrete equalities should compute before trying coordinate rewriting or
    general arithmetic search. Project away length proofs before reducing,
    split only list constructors, and leave the context untouched. Decimal
    literals use [Q2R]; expose their exact fractions for the field normalizer.
    The surrounding [solve] requires every denominator obligation to close. *)
Ltac vec_compute :=
  solve [apply vector_eq;
    repeat first
      [ apply vector_eq
      | match goal with |- @eq (list _) (_ :: _) (_ :: _) => f_equal end
      | progress cbn ];
    cbv beta iota zeta delta
      [add mul scale zero one Add_R Mul_R Scale_R Zero_R One_R
       Add_nat Mul_nat Scale_nat Zero_nat One_nat];
    solve [reflexivity | ring |
      cbv beta iota zeta delta [Q2R Qnum Qden]; field]].

Ltac auto_vec_core :=
  repeat first
    [ progress (cbv beta iota zeta delta
        [add mul scale vector_dot vector_fold vector_map2 vector_map
         Add_Vector Mul_Vector Scale_Vector Add_R Mul_R Scale_R
         Add_nat Mul_nat Scale_nat] in *)
    | progress simpl in *
    | apply vector_eq
    | match goal with |- @eq (list _) (_ :: _) (_ :: _) => f_equal end
    | solve [auto_op] ].

Ltac auto_vec :=
  solve [ vec_compute | auto_op | auto_vec_core ].

Section Vector_Examples.
  Section R_Examples.
    Local Open Scope R_scope.
    Local Open Scope V_Scope.
    
    Let v1 := ⟨1, 2, 3⟩.
    Let v2 := ⟨4, 5, 6⟩.

    Example vector_add_example : v1 + v2 = ⟨5, 7, 9⟩.
    Proof.
      auto_vec.
    Qed.
  End R_Examples.

  Section Nat_Examples.
    Local Open Scope nat_scope.
    Local Open Scope V_Scope.
    Let v1 := ⟨1, 2, 3⟩.
    Let v2 := ⟨4, 5, 6⟩.

    Example vector_add_example_nat : v1 + v2 = ⟨5, 7, 9⟩.
    Proof.
      auto_vec.
    Qed.

    Example vector_dot_example_nat : v1 · v2 = 32%nat.
    Proof.
      auto_vec.
    Qed.

  End Nat_Examples.

End Vector_Examples.

Section Vector_Theorems.
  Local Open Scope R_scope.
  Local Open Scope V_Scope.

  Lemma vector_add_comm_R {n : nat} (v1 v2 : vector R n) :
  v1 + v2 = v2 + v1.
  Proof.
    destruct v1 as [l1 H1], v2 as [l2 H2].
    apply vector_eq. simpl.
    revert n l2 H1 H2. induction l1 as [|h1 t1 H3]; intros n l2 H1 H2.
    - destruct l2 as [|h2 t2]; [reflexivity | inversion H2]; reflexivity.
    - destruct l2 as [|h2 t2]; [inversion H2; reflexivity | ].
      simpl in *. f_equal.
      + apply Rplus_comm.
      + apply (H3 (length t1) t2); congruence.
  Qed.

  Lemma vector_add_assoc_R {n : nat} (v1 v2 v3 : vector R n) :
  (v1 + v2) + v3 = v1 + (v2 + v3).
  Proof.
    destruct v1 as [l1 H1], v2 as [l2 H2], v3 as [l3 H3].
    apply vector_eq. simpl.
    revert n l2 l3 H1 H2 H3. induction l1 as [|h1 t1 H4]; intros n l2 l3 H1 H2 H3.
    - destruct l2; [|inversion H2]; reflexivity.
    - destruct l2 as [|h2 t2]; [inversion H2|]; try reflexivity.
      destruct l3 as [|h3 t3]; [inversion H3|]; try reflexivity.
      simpl in *. f_equal.
      + apply Rplus_assoc.
      + apply (H4 (length t1) t2 t3); congruence.
  Qed.

  Lemma vector_add_id_r_R {n : nat} (v : vector R n) :
  v + 0 = v.
  Proof.
    destruct v as [l1 H1]. unfold zero, Zero_Vector.
    apply vector_eq. simpl.
    revert n H1. induction l1 as [|h1 t1 H2]; intros n H1.
    - destruct n as [|H3]; [reflexivity | inversion H1].
    - destruct n as [|H3]; [inversion H1 |].
      simpl in *. f_equal.
      + apply Rplus_0_r.
      + apply H2. congruence.
  Qed.

  Lemma vector_dot_comm_R {n : nat} (v1 v2 : vector R n) :
  v1 · v2 = v2 · v1.
  Proof.
    destruct v1 as [l1 H1], v2 as [l2 H2].
    unfold vector_dot, vector_fold, vector_map2, mul, Mul_R. simpl.
    revert n l2 H1 H2. induction l1 as [|h1 t1 H3]; intros n l2 H1 H2.
    - destruct l2 as [|h2 t2]; [ | inversion H2]; reflexivity.
    - destruct l2 as [|h2 t2]; [inversion H2; reflexivity | ]. 
      simpl in *. rewrite Rmult_comm. f_equal.
      apply (H3 (length t1) t2); congruence.
  Qed.

  Lemma vector_dot_distr_R {n : nat} (v1 v2 v3 : vector R n) :
  v1 · (v2 + v3) = (v1 · v2) + (v1 · v3).
Proof.
  destruct v1 as [l1 H1], v2 as [l2 H2], v3 as [l3 H3].
  unfold vector_dot, vector_fold, vector_map2. simpl.
  revert n l2 l3 H1 H2 H3. induction l1 as [|h1 t1 H4]; intros n l2 l3 H1 H2 H3; try auto_vec.
  destruct l2 as [|h2 t2]; [inversion H2|]; try auto_vec.
  destruct l3 as [|h3 t3]; [inversion H3|]; try auto_vec.
  simpl in *. rewrite (H4 (length t1) t2 t3); try congruence.
  unfold zero, Zero_R, add, Add_R, mul, Mul_R in *. lra.
Qed.

  Definition vector_nth {A : Type} {n : nat} `{Zero A} (v : vector A n) (i : nat) (H1 : i < n) : A.
  Proof.
    destruct v as [l1 H2].
    exact (nth i l1 zero).
  Defined.

  Lemma vector_ext {A : Type} {n : nat} `{Zero A} (v1 v2 : vector A n) :
  (forall i (H1 : i < n), vector_nth v1 i H1 = vector_nth v2 i H1) -> v1 = v2.
Proof.
  destruct v1 as [l1 H1], v2 as [l2 H2].
  intros H4. apply vector_eq. simpl.
  revert n l2 H1 H2 H4. induction l1 as [|h1 t1 H3]; intros n l2 H1 H2 H4.
  - destruct l2 as [|h2 t2]; [reflexivity | simpl in H1, H2; lia].
  - destruct l2 as [|h2 t2]; [simpl in H1, H2; lia |].
    destruct n as [|n']; [simpl in H1; lia |].
    f_equal.
    + unfold lt_op, Lt_nat in *.
      assert (H5 : (0 < S n')%nat) by lia.
      exact (H4 0%nat H5).
    + assert (H0 : length t1 = n') by (simpl in H1; lia).
      assert (H5 : length t2 = n') by (simpl in H2; lia).
      apply (H3 n' t2 H0 H5).
      intros i H6.
      unfold lt_op, Lt_nat in *.
      assert (H7 : (S i < S n')%nat) by lia.
      exact (H4 (S i) H7).
Qed.

End Vector_Theorems.

(** A proof-independent coordinate accessor for rewriting symbolic expressions. *)
Definition vector_coord {A n} `{Zero A} (v : vector A n) (i : nat) : A :=
  List.nth i (vlist v) zero.

Lemma vector_coord_ext {A n} `{Zero A} (v w : vector A n) :
  (forall i, (i < n)%nat -> vector_coord v i = vector_coord w i) -> v = w.
Proof.
  destruct v as [v Hv], w as [w Hw]. intros Hcoord.
  apply vector_ext. intros i Hi. exact (Hcoord i Hi).
Qed.

Lemma vector_empty_eq {A} (v w : vector A 0) : v = w.
Proof.
  destruct v as [v Hv], w as [w Hw]. apply vector_eq. simpl.
  destruct v, w; simpl in *; congruence.
Qed.

Lemma vector_coord_map {A B n} `{Zero A} `{Zero B} (f : A -> B) (v : vector A n) i :
  (i < n)%nat -> vector_coord (vector_map f v) i = f (vector_coord v i).
Proof.
  destruct v as [l Hl]. unfold vector_coord, vector_map. simpl.
  subst n. revert i. induction l as [|x xs IH]; intros [|i] Hi; simpl in *; try lia; auto.
  apply IH. lia.
Qed.

Lemma vector_coord_map2 {A B C n} `{Zero A} `{Zero B} `{Zero C}
    (f : A -> B -> C) (v : vector A n) (w : vector B n) i :
  (i < n)%nat -> vector_coord (vector_map2 f v w) i = f (vector_coord v i) (vector_coord w i).
Proof.
  destruct v as [v Hv], w as [w Hw]. unfold vector_coord, vector_map2. simpl.
  subst n. revert w Hw i. induction v as [|x xs IH]; intros [|y ys] Hw [|i] Hi;
    simpl in *; try lia; auto.
  apply IH; lia.
Qed.

Lemma vector_coord_zero {A n} `{Zero A} i :
  vector_coord (zero : vector A n) i = (zero : A).
Proof.
  unfold vector_coord, zero, Zero_Vector. simpl.
  revert i. induction n; intros [|i]; simpl; auto.
Qed.

Lemma vector_coord_add {A n} `{Zero A} `{Add A} (v w : vector A n) i :
  (i < n)%nat -> vector_coord (add v w) i = add (vector_coord v i) (vector_coord w i).
Proof. apply vector_coord_map2. Qed.

Lemma vector_coord_mul {A n} `{Zero A} `{Mul A} (v w : vector A n) i :
  (i < n)%nat -> vector_coord (mul v w) i = mul (vector_coord v i) (vector_coord w i).
Proof. apply vector_coord_map2. Qed.

Lemma vector_coord_scale {S A n} `{Zero A} `{Scale S A} (s : S) (v : vector A n) i :
  (i < n)%nat -> vector_coord (scale s v) i = scale s (vector_coord v i).
Proof. apply vector_coord_map. Qed.

Create HintDb vector_coords.
#[export] Hint Rewrite @vector_coord_zero : vector_coords.
#[export] Hint Rewrite @vector_coord_add @vector_coord_mul @vector_coord_scale
  @vector_coord_map @vector_coord_map2 using solve [lia] : vector_coords.

(** Extensions supplied by the functional representation are tried without
    introducing a dependency from this list library back to that representation. *)
Ltac vector_solver_extension := fail.

Ltac linear_scalar :=
  cbv beta iota zeta delta [add mul scale zero one Add_R Mul_R Scale_R Zero_R One_R
    Add_nat Mul_nat Scale_nat Zero_nat One_nat] in *;
  solve [reflexivity | assumption | ring | congruence | eauto 3 | lra | nra | lia | solve_R].

Ltac vector_coords_simpl :=
  repeat first [progress autorewrite with vector_coords
               | rewrite vector_coord_map by lia
               | rewrite vector_coord_map2 by lia].

(** [vec_simpl] leaves coordinate goals available for manual proof. *)
Ltac vec_simpl :=
  vector_coords_simpl;
  repeat first
    [ apply vector_empty_eq
    | apply vector_coord_ext; let i := fresh "i" in let Hi := fresh "Hi" in
      intros i Hi; vector_coords_simpl ].

Ltac solve_vec :=
  solve [intros;
    first [solve [vec_compute]
          | solve [vector_solver_extension]
          | solve [vec_simpl; linear_scalar]
          | solve [auto_op | auto_vec_core]]].

Ltac auto_vec ::= solve_vec.

Lemma vector_scale_zero_R {n} (v : vector R n) : scale 0%R v = zero.
Proof. solve_vec. Qed.

Lemma vector_scale_one_R {n} (v : vector R n) : scale 1%R v = v.
Proof. solve_vec. Qed.

Lemma vector_scale_add_R {n} (a b : R) (v : vector R n) :
  scale (a + b)%R v = add (scale a v) (scale b v).
Proof. solve_vec. Qed.

Lemma vector_scale_distr_R {n} (a : R) (v w : vector R n) :
  scale a (add v w) = add (scale a v) (scale a w).
Proof. solve_vec. Qed.

Lemma vector_scale_assoc_R {n} (a b : R) (v : vector R n) :
  scale a (scale b v) = scale (a * b)%R v.
Proof. solve_vec. Qed.

Lemma vector_dot_distr_l_R {n} (u v w : vector R n) :
  vector_dot (add u v) w = (vector_dot u w + vector_dot v w)%R.
Proof. rewrite vector_dot_comm_R, vector_dot_distr_R. unfold add, Add_R. f_equal; apply vector_dot_comm_R. Qed.

Lemma vector_dot_scale_l_R {n} (a : R) (v w : vector R n) :
  vector_dot (scale a v) w = (a * vector_dot v w)%R.
Proof.
  destruct v as [v Hv], w as [w Hw].
  unfold vector_dot, vector_fold, vector_map2, scale, Scale_Vector, vector_map.
  cbv beta iota zeta delta [vlist add Add_R mul Mul_R scale Scale_R zero Zero_R].
  subst n. revert w Hw. induction v as [|x xs IH]; intros [|y ys] Hw; simpl in *; try discriminate.
  - ring.
  - rewrite IH by lia. ring.
Qed.

Lemma vector_dot_scale_r_R {n} (a : R) (v w : vector R n) :
  vector_dot v (scale a w) = (a * vector_dot v w)%R.
Proof. rewrite vector_dot_comm_R, vector_dot_scale_l_R, (vector_dot_comm_R w v). reflexivity. Qed.

Lemma vector_dot_zero_l_R {n} (v : vector R n) : vector_dot zero v = 0%R.
Proof.
  rewrite <- (vector_scale_zero_R v), vector_dot_scale_l_R. ring.
Qed.

Lemma vector_dot_zero_r_R {n} (v : vector R n) : vector_dot v zero = 0%R.
Proof. rewrite vector_dot_comm_R. apply vector_dot_zero_l_R. Qed.

Lemma vector_dot_empty_R (v w : vector R 0) : vector_dot v w = 0%R.
Proof. rewrite (vector_empty_eq v zero). apply vector_dot_zero_l_R. Qed.

Create HintDb vector_algebra.
#[export] Hint Rewrite @vector_dot_distr_R @vector_dot_distr_l_R
  @vector_dot_scale_l_R @vector_dot_scale_r_R @vector_dot_zero_l_R @vector_dot_zero_r_R
  vector_dot_empty_R
  : vector_algebra.

Ltac vector_dot_symmetry :=
  repeat match goal with
  | |- context [@vector_dot R ?n Add_R Mul_R Zero_R ?v ?w] =>
      first [constr_eq v w; fail 1 |
        match goal with
        | |- context [@vector_dot R n Add_R Mul_R Zero_R w v] =>
            replace (vector_dot v w) with (vector_dot w v) by apply vector_dot_comm_R
        end]
  end.

Ltac solve_vec ::=
  solve [intros;
    first [solve [reflexivity | assumption]
          | solve [vec_compute]
          | solve [vector_solver_extension]
          | solve [autorewrite with vector_algebra; vector_dot_symmetry;
              vec_simpl; linear_scalar]
          | solve [unfold vector_norm; f_equal;
              autorewrite with vector_algebra; vector_dot_symmetry; linear_scalar]
          | solve [auto_op | auto_vec_core; linear_scalar]]].
